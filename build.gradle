plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

apply from: rootProject.file('extras.gradle')

group GROUP
version VERSION

sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()

    maven {
        url('https://frcmaven.wpi.edu/artifactory/release')
    }
    maven {
        url('https://maven.revrobotics.com/')
    }
    maven {
        url('https://maven.ctr-electronics.com/release/')
    }
}

dependencies {
    compileOnly group: 'edu.wpi.first.wpilibj', name: 'wpilibj-java', version: "$WPILIB_VERSION"
    compileOnly group: 'edu.wpi.first.wpiutil', name: 'wpiutil-java', version: "$WPILIB_VERSION"
    compileOnly group: 'edu.wpi.first.wpilibNewCommands', name: 'wpilibNewCommands-java', version: "$WPILIB_VERSION"

    compileOnly group: 'com.revrobotics.frc', name: 'REVLib-java', version: "$REVLIB_VERSION"

    compileOnly group: 'com.ctre.phoenix', name: 'wpiapi-java', version: "$PHOENIX_V5_VERSION"
    compileOnly group: 'com.ctre.phoenix', name: 'api-java', version: "$PHOENIX_V5_VERSION"
    compileOnly group: 'com.ctre.phoenix6', name: 'wpiapi-java', version: "$PHOENIX_V6_VERSION"
}

jar {
    archiveBaseName.set(project.name)
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveBaseName.set(project.name)
    archiveClassifier.set('sources')

    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveBaseName.set(project.name)
    archiveClassifier.set('javadoc')

    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'frc-simlib'
                description = 'Components for Simulation for FRC Java Robots'
                url = 'https://github.com/tomtzook/frc-simlib'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = ' http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id = 'tomtzook'
                        name = 'Tom Tzook'
                        email = 'tomtzook@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/tomtzook/frc-simlib.git'
                    developerConnection = 'scm:git:ssh://github.com/tomtzook/frc-simlib.git'
                    url = 'https://github.com/tomtzook/frc-simlib'
                }
            }
        }
    }

    repositories {
        maven {
            if (isReleaseBuild()) {
                name = 'NexusStaging'
                url = NEXUS_RELEASE_REPOSITORY_URL
            } else {
                name = 'NexusSnapshot'
                url = NEXUS_SNAPSHOT_REPOSITORY_URL
            }

            credentials {
                username getNexusUsername()
                password getNexusPassword()
            }
        }
    }
}

if (shouldSign()) {
    signing {
        sign publishing.publications.mavenJava
    }
}
